// config for dependencies
ext {
    libsVersion = [support : '26.+',
                   retrofit: '2.3.0']

    libs = [supportAnnotations: "com.android.support:support-annotations:${libsVersion.support}",
            recyclerviewV7    : "com.android.support:recyclerview-v7:${libsVersion.support}",
            cardviewV7        : "com.android.support:cardview-v7:${libsVersion.support}",
            appcompatV7       : "com.android.support:appcompat-v7:${libsVersion.support}",
            design            : "com.android.support:design:${libsVersion.support}",
            supportV4         : "com.android.support:support-v4:${libsVersion.support}",
            preferenceV7      : "com.android.support:preference-v7:${libsVersion.support}"]
}

// config for android config
def versionCode() {
    def cmd = 'git rev-list HEAD --no-merges --count'
    cmd.execute().text.trim().toInteger()
}

def versionName() {  //自动版本名
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "-" + version.substring(matcher.end(), version.length())
    }

    return version
}

// default config for android plugin
subprojects {
    project.plugins.whenPluginAdded { plugin ->
        switch (plugin.class.name) {
            case 'com.android.build.gradle.AppPlugin':
                project.android {
                }

                project.android.applicationVariants.all { variant ->
                    variant.mergedFlavor.versionCode = versionCode()
                    variant.mergedFlavor.versionName = versionName()
                }

        // fall through
            case 'com.android.build.gradle.LibraryPlugin':
                project.android {
                    compileSdkVersion 26
                    buildToolsVersion '26.0.2'

                    defaultConfig {
                        minSdkVersion 19
                        targetSdkVersion 26
                    }
                }
                break
        }
    }
}